!classDefinition: #PortfolioTest category: 'Portfolio-Solucion'!
TestCase subclass: #PortfolioTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:16:26'!
test01BalanceOfPortfolioWithoutAccountsIsZero

	self assert: 0 equals: Portfolio new balance! !

!PortfolioTest methodsFor: 'tests' stamp: 'NR 5/27/2021 17:36:04'!
test02BalanceOfPortfolioWithAccountsIsSumOfAccountsBalance

	| account portfolio |
	
	account := ReceptiveAccount new.
	Deposit register: 100 on: account.
	portfolio := Portfolio with: account.
	
	self assert: account balance equals: portfolio balance! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:23:25'!
test03BalanceOfPortfolioIsCalculatedRecursivelyOnPortfolios

	| simplePortfolioAccount simplePortfolio composedPortfolioAccount composedPortofolio |
	
	simplePortfolioAccount := ReceptiveAccount new.
	Deposit register: 100 on: simplePortfolioAccount.
	simplePortfolio := Portfolio with: simplePortfolioAccount.
	
	composedPortfolioAccount := ReceptiveAccount new.
	Withdraw register: 50 on: composedPortfolioAccount.
	composedPortofolio := Portfolio with: simplePortfolio with: composedPortfolioAccount.
	
	self assert: (composedPortfolioAccount balance + simplePortfolio balance) equals: composedPortofolio balance! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:43:15'!
test04PortfolioWithoutAccountsHasNoRegisteredTransaction

	self deny: (Portfolio new hasRegistered: (Deposit for: 100))! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:43:11'!
test05PortfolioHasRegisteredItsAccountsTransactions

	| account portfolio deposit |
	
	account := ReceptiveAccount new.
	deposit := Deposit register: 100 on: account.
	portfolio := Portfolio with: account.
	
	self assert: (portfolio hasRegistered: deposit)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:06'!
test06PortfolioLooksForRegisteredTransactionsRecursively

	| simplePortfolioAccount simplePortfolio composedPortfolioAccount composedPortfolio composedPortfolioAccountWithdraw simplePortfolioAccountDeposit |
	
	simplePortfolioAccount := ReceptiveAccount new.
	simplePortfolioAccountDeposit := Deposit register: 100 on: simplePortfolioAccount.
	simplePortfolio := Portfolio with: simplePortfolioAccount.
	
	composedPortfolioAccount := ReceptiveAccount new.
	composedPortfolioAccountWithdraw := Withdraw register: 50 on: composedPortfolioAccount.
	composedPortfolio := Portfolio with: simplePortfolio with: composedPortfolioAccount.
	
	self assert: (composedPortfolio hasRegistered: simplePortfolioAccountDeposit).
	self assert: (composedPortfolio hasRegistered: composedPortfolioAccountWithdraw)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:10'!
test07PortfolioHasNoTransactionWhenHasNoAccounts

	self assert: Portfolio new transactions isEmpty! !

!PortfolioTest methodsFor: 'tests' stamp: 'NR 6/22/2020 07:31:19'!
test08PortfolioTransactionsIncludesAllItsAccountsTransactions

	| account portfolio accountDeposit anotherAccount portfolioTransactions anotherAccountWithdraw |
	
	account := ReceptiveAccount new.
	accountDeposit := Deposit register: 100 on: account.
	anotherAccount := ReceptiveAccount new.
	anotherAccountWithdraw := Withdraw register: 100 on: account.
	portfolio := Portfolio with: account.
	
	portfolioTransactions := portfolio transactions.
	
	self assert: 2 equals: portfolioTransactions size.
	self assert: (portfolioTransactions includes: accountDeposit).
	self assert: (portfolioTransactions includes: anotherAccountWithdraw)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:20'!
test09PortfolioTransactionsAreCalculatedRecursively

	| simplePortfolioAccount simplePortfolio composedPortfolioAccount composedPortfolio composedPortfolioAccountWithdraw simplePortfolioAccountDeposit composedPortfolioTransactions |
	
	simplePortfolioAccount := ReceptiveAccount new.
	simplePortfolioAccountDeposit := Deposit register: 100 on: simplePortfolioAccount.
	simplePortfolio := Portfolio with: simplePortfolioAccount.
	
	composedPortfolioAccount := ReceptiveAccount new.
	composedPortfolioAccountWithdraw := Withdraw register: 50 on: composedPortfolioAccount.
	composedPortfolio := Portfolio with: simplePortfolio with: composedPortfolioAccount.
	
	composedPortfolioTransactions := composedPortfolio transactions.
	self assert: 2 equals: composedPortfolioTransactions size.
	self assert: (composedPortfolioTransactions includes: simplePortfolioAccountDeposit).
	self assert: (composedPortfolioTransactions includes: composedPortfolioAccountWithdraw)! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:24'!
test10PortfolioCanNotIncludeTheSameAccountMoreThanOnce

	| account portfolio |
	
	account := ReceptiveAccount new.
	portfolio := Portfolio with: account.
	
	self 
		should: [ portfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: 1 equals: portfolio accountsSize.
			self assert: (portfolio accountsIncludes: account) ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:28'!
test11PortfolioCanNotIncludeAccountOfItsPortfolios

	| account simplePortfolio composedPortfolio |
	
	account := ReceptiveAccount new.
	simplePortfolio := Portfolio with: account.
	composedPortfolio := Portfolio with: simplePortfolio.
	
	self 
		should: [ composedPortfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: 1 equals: composedPortfolio accountsSize.
			self assert: (composedPortfolio accountsIncludes: simplePortfolio) ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 11:58:32'!
test12PortfolioCanNotIncludeItself

	| account simplePortfolio |
	
	account := ReceptiveAccount new.
	simplePortfolio := Portfolio with: account.
	
	self 
		should: [ simplePortfolio add: simplePortfolio ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: 1 equals: simplePortfolio accountsSize.
			self assert: (simplePortfolio accountsIncludes: account) ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 12:01:51'!
test13ComposedPortfolioCanNotHaveParentPortfolioAccount

	| account simplePortfolio composedPortfolio |
	
	account := ReceptiveAccount new.
	simplePortfolio := Portfolio new.
	composedPortfolio := Portfolio with: simplePortfolio.
	composedPortfolio add: account.
	
	self 
		should: [ simplePortfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: simplePortfolio accountsIsEmpty ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/25/2019 12:12:16'!
test14ComposedPortfolioCanNotHaveAccountOfAnyRootParentRecursively

	| account leftParentPortfolio leftRootParentPortfolio portfolio rightParentPortfolio rightRootParentPortfolio |
	
	account := ReceptiveAccount new.
	portfolio := Portfolio new.
	leftParentPortfolio := Portfolio with: portfolio .
	leftRootParentPortfolio := Portfolio with: leftParentPortfolio.
	leftRootParentPortfolio add: account.
	
	rightParentPortfolio := Portfolio with: portfolio .
	rightRootParentPortfolio := Portfolio with: rightParentPortfolio.
	rightRootParentPortfolio add: account.

	self 
		should: [ portfolio add: account ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError |
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: portfolio accountsIsEmpty ]! !

!PortfolioTest methodsFor: 'tests' stamp: 'HAW 5/29/2019 16:31:18'!
test15PortfolioCanNotIncludeAnyOfTheComposedAccountOfPortfolioToAdd

	| portfolioToAdd portfolioToModify rootPortfolio sharedAccount |
	
	sharedAccount := ReceptiveAccount new.
	portfolioToModify := Portfolio new.
	rootPortfolio := Portfolio with: sharedAccount with: portfolioToModify.
	portfolioToAdd := Portfolio with: sharedAccount.
	
	self 
		should: [ portfolioToModify add: portfolioToAdd ]
		raise: Error - MessageNotUnderstood 
		withExceptionDo: [ :anError | 
			self assert: Portfolio canNotAddAccountErrorMessage equals: anError messageText.
			self assert: portfolioToModify accountsIsEmpty ]! !


!classDefinition: #ReceptiveAccountTest category: 'Portfolio-Solucion'!
TestCase subclass: #ReceptiveAccountTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:19:48'!
test01ReceptiveAccountHaveZeroAsBalanceWhenCreated 

	| account |
	
	account := ReceptiveAccount new.

	self assert: 0 equals: account balance .
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:19:54'!
test02DepositIncreasesBalanceOnTransactionValue 

	| account |
	
	account := ReceptiveAccount  new.
	Deposit register: 100 on: account.
		
	self assert: 100 equals: account balance .
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:02'!
test03WithdrawDecreasesBalanceOnTransactionValue 

	| account |
	
	account := ReceptiveAccount new.
	Deposit register: 100 on: account.
	Withdraw register: 50 on: account.
		
	self assert: 50 equals: account balance .
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:32'!
test04WithdrawValueMustBePositive 

	| account withdrawValue |
	
	account := ReceptiveAccount new.
	withdrawValue := 50.
	
	self assert: withdrawValue equals: (Withdraw register: withdrawValue on: account) value
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:46'!
test05ReceptiveAccountKnowsRegisteredTransactions 

	| account deposit withdraw |
	
	account := ReceptiveAccount new.
	deposit := Deposit register: 100 on: account.
	withdraw := Withdraw register: 50 on: account.
		
	self assert: (account hasRegistered: deposit).
	self assert: (account hasRegistered: withdraw).
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:20:54'!
test06ReceptiveAccountDoNotKnowNotRegisteredTransactions

	| account deposit withdraw |
	
	account := ReceptiveAccount new.
	deposit :=  Deposit for: 100.
	withdraw := Withdraw for: 50.
		
	self deny: (account hasRegistered: deposit).
	self deny: (account hasRegistered:withdraw).
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'HAW 5/23/2019 15:21:24'!
test07AccountKnowsItsTransactions 

	| account1 deposit1 |
	
	account1 := ReceptiveAccount new.
	
	deposit1 := Deposit register: 100 on: account1.
		
	self assert: 1 equals: account1 transactions size .
	self assert: (account1 transactions includes: deposit1).
! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'LB 10/21/2021 10:44:39'!
test08BalanceWithATransferIsCorrect

	|sender receiver transfer|
	
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	transfer := Transfer from: sender to: receiver for: 100.
	
	self assert: -100 equals: sender balance.
	self assert: 100 equals: receiver balance.! !

!ReceptiveAccountTest methodsFor: 'tests' stamp: 'LB 10/21/2021 10:45:36'!
test09HasRegisteredATransferIsCorrect

	|sender receiver transfer|
	
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	transfer := Transfer from: sender to: receiver for: 100.
	
	self assert: (sender hasRegistered: transfer sendLeg ).
	self assert: (receiver hasRegistered: transfer receiveLeg ).! !


!classDefinition: #ReportsTest category: 'Portfolio-Solucion'!
TestCase subclass: #ReportsTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:08:00'!
test01AccountSummaryForEmptyAccount
	| account report |
	account := ReceptiveAccount new.
	report := (AccountSummary for: account) do.
	
	self assert: 1 equals: (report size).
	self assert: 'Balance = 0' equals: (report first).! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:08:38'!
test02AccountSummaryForOneDepositInReceptiveAccount
	| account report |
	account := ReceptiveAccount new.
	Deposit register: 100 on: account.
	
	report := (AccountSummary for: account) do.
	
	self assert: 2 equals: (report size).
	self assert: 'Depósito por 100' equals: (report at: 1).
	self assert: 'Balance = 100' equals: (report at: 2).! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:08:58'!
test03AccountSummaryForOneWithdrawInReceptiveAccount
	| account report |
	account := ReceptiveAccount new.
	Withdraw register: 100 on: account.
	
	report := (AccountSummary for: account) do.
	
	self assert: 2 equals: (report size).
	self assert: 'Extracción por 100' equals: (report at: 1).
	self assert: 'Balance = -100' equals: (report at: 2).! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:09:17'!
test04AccountSummaryForOneSendInReceptiveAccount
	| sender receiver report |
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	Transfer from: sender to: receiver for: 100.
	
	report := (AccountSummary for: sender) do.
	
	self assert: 2 equals: (report size).
	self assert: 'Salida por transferencia de 100' equals: (report at: 1).
	self assert: 'Balance = -100' equals: (report at: 2).! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:09:33'!
test05AccountSummaryForOneReceiveInReceptiveAccount
	| sender receiver report |
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	Transfer from: sender to: receiver for: 100.
	
	report := (AccountSummary for: receiver) do.
	
	self assert: 2 equals: (report size).
	self assert: 'Entrada por transferencia de 100' equals: (report at: 1).
	self assert: 'Balance = 100' equals: (report at: 2).! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:09:54'!
test06AccountSummaryForManyTransactionsInReceptiveAccount
	| account report |
	account := ReceptiveAccount new.
	Deposit register: 50 on: account.
	Deposit register: 50 on: account.
	
	report := (AccountSummary for: account) do.
	
	self assert: 3 equals: (report size).
	self assert: 'Depósito por 50' equals: (report at: 1).
	self assert: 'Depósito por 50' equals: (report at: 2).
	self assert: 'Balance = 100' equals: (report at: 3).! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:14:15'!
test11AccountSummaryForEmptyPortfolio
	| report portfolio |
	portfolio := Portfolio new.
	
	report := (AccountSummary for: portfolio) do.
	self assert: 1 equals: (report size)	.
	self assert: 'Balance = 0' equals: (report at: 1)	.! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:14:29'!
test12AccountSummaryForPortfolioWithOneReceptiveAccount
	| account report portfolio |
	portfolio := Portfolio new.
	account := ReceptiveAccount new.
	portfolio add: account.
	
	Deposit register: 100 on: account.
	Withdraw register: 50 on: account.
	Transfer from: account to: ReceptiveAccount new for: 20. 
	Transfer from: ReceptiveAccount new to: account for: 30.
	report := (AccountSummary for: portfolio) do.
	self assert: 5 equals: (report size)	.
	self assert: 'Depósito por 100' equals: (report at: 1)	.
	self assert: 'Extracción por 50' equals: (report at: 2)	.
	self assert: 'Salida por transferencia de 20' equals: (report at: 3)	.
	self assert: 'Entrada por transferencia de 30' equals: (report at: 4)	.
	self assert: 'Balance = 60' equals: (report at: 5)	.! !

!ReportsTest methodsFor: 'account summary - tests' stamp: 'LB 10/24/2021 22:15:17'!
test13AccountSummaryForPortfolioWithReceptiveAccountAndAnotherPortfolio
	| account anotherAccount report rootPortfolio leafPortfolio |
	rootPortfolio := Portfolio new.
	leafPortfolio := Portfolio new.
	account := ReceptiveAccount new.
	anotherAccount := ReceptiveAccount new.
	leafPortfolio add: account.
	
	rootPortfolio add: leafPortfolio.
	rootPortfolio add: anotherAccount.
	
	Transfer from: account to: anotherAccount for: 100.
	report := (AccountSummary for: rootPortfolio) do.
	self assert: 3 equals: (report size)	.
	self assert: report includes: 'Salida por transferencia de 100'	.
	self assert: report includes: 'Entrada por transferencia de 100'.
	self assert: 'Balance = 0' equals: (report last)	.! !


!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:10:40'!
test07TransferNetForEmptyAccount
	| account report |
	account := ReceptiveAccount new.
	
	report := (TransferNet for: account) do.
	
	self assert: 0 equals: report.
	! !

!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:11:16'!
test08TransferNetForOneSendInReceptiveAccount
	| sender receiver report |
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	Transfer from: sender to: receiver for: 100.
	
	report := (TransferNet for: sender) do.
	
	self assert: (0-100) equals: report.
	! !

!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:11:47'!
test09TransferNetForOneReceiveInReceptiveAccount
	| sender receiver report |
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	Transfer from: sender to: receiver for: 100.
	
	report := (TransferNet for: receiver) do.
	
	self assert: 100 equals: report.
	! !

!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:12:38'!
test10TransferNetForManyTransactionsInReceptiveAccount
	| sender receiver report |
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	Transfer from: sender to: receiver for: 100.
	Transfer from: sender to: receiver for: 100.
	
	report := (TransferNet for: sender) do.
	
	self assert: (0-200) equals: report.
	! !

!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:22:54'!
test14TransferNetForEmptyPortfolio
	| report portfolio |
	portfolio := Portfolio new.
	
	report := (TransferNet for: portfolio) do.
	self assert: 0 equals: report! !

!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:23:20'!
test15transferNetForPortfolioWithOneReceptiveAccount
	| account report portfolio |
	portfolio := Portfolio new.
	account := ReceptiveAccount new.
	portfolio add: account.
	
	Deposit register: 100 on: account.
	Withdraw register: 50 on: account.
	Transfer from: account to: ReceptiveAccount new for: 20. 
	Transfer from: ReceptiveAccount new to: account for: 30.
	report := (TransferNet for: portfolio) do.
	self assert: 10 equals: report.! !

!ReportsTest methodsFor: 'tranfer net - tests' stamp: 'LB 10/24/2021 22:23:58'!
test16transferNetForPortfolioWithReceptiveAccountAndAnotherPortfolio
	| account anotherAccount report rootPortfolio leafPortfolio |
	rootPortfolio := Portfolio new.
	leafPortfolio := Portfolio new.
	account := ReceptiveAccount new.
	anotherAccount := ReceptiveAccount new.
	leafPortfolio add: account.
	
	rootPortfolio add: leafPortfolio.
	rootPortfolio add: anotherAccount.
	
	Transfer from: account to: ReceptiveAccount new for: 100.
	Transfer from: anotherAccount to: ReceptiveAccount new for: 100.
	Transfer from: ReceptiveAccount new to: anotherAccount for: 10.
	
	report := (TransferNet for: rootPortfolio) do.
	self assert: -190 equals: report.! !


!classDefinition: #TransferTest category: 'Portfolio-Solucion'!
TestCase subclass: #TransferTest
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!TransferTest methodsFor: 'tests' stamp: 'LB 10/24/2021 22:08:00'!
test01AccountSummaryForEmptyAccount
	
	|sender receiver transfer|
	
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	transfer := Transfer from: sender to: receiver for: 100.
	
	self assert: (transfer sendLeg transfer) equals: transfer.
	self assert: (transfer receiveLeg transfer) equals: transfer.
	self assert: transfer amount equals: 100.
	
	! !

!TransferTest methodsFor: 'tests' stamp: 'LB 10/24/2021 22:08:38'!
test02AccountSummaryForOneDepositInReceptiveAccount
	
	|sender receiver transfer|
	
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	transfer := Transfer from: sender to: receiver for: 100.
	
	self assert: transfer amount equals: 100.
	! !

!TransferTest methodsFor: 'tests' stamp: 'LB 10/24/2021 22:08:58'!
test03AccountSummaryForOneWithdrawInReceptiveAccount
	
	|sender receiver|
	
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	
	self should:
		[Transfer from: sender to: receiver for: 0]
		raise: Error
		withMessageText:['No se puede hacer una transferencia de valor 0']
	! !

!TransferTest methodsFor: 'tests' stamp: 'LB 10/24/2021 22:09:17'!
test04AccountSummaryForOneSendInReceptiveAccount
	
	|sender |
	
	sender := ReceptiveAccount new.
	
	self should:
		[Transfer from: sender to: sender for: 100]
		raise: Error
		withMessageText:['No se puede hacer una transferencia a si mismo']
	! !

!TransferTest methodsFor: 'tests' stamp: 'LB 10/24/2021 22:09:33'!
test05AccountSummaryForOneReceiveInReceptiveAccount
	
	|sender receiver|
	
	sender := ReceptiveAccount new.
	receiver := ReceptiveAccount new.
	
	self should:
		[Transfer from: sender to: receiver for: (0-50)]
		raise: Error
		withMessageText:['No se puede hacer una transferencia negativa']
	! !


!classDefinition: #Account category: 'Portfolio-Solucion'!
Object subclass: #Account
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Account methodsFor: 'testing' stamp: 'HAW 5/25/2019 12:23:47'!
hasRegistered: aTransaction

	self subclassResponsibility ! !

!Account methodsFor: 'testing' stamp: 'HAW 5/25/2019 12:24:25'!
isComposedBy: anAccount

	self subclassResponsibility ! !


!Account methodsFor: 'balance' stamp: 'HAW 5/25/2019 12:23:40'!
balance

	self subclassResponsibility ! !


!Account methodsFor: 'transactions' stamp: 'HAW 5/25/2019 12:23:27'!
addTransactionsTo: aCollectionOfTransactions

	self subclassResponsibility ! !

!Account methodsFor: 'transactions' stamp: 'HAW 5/25/2019 12:23:15'!
transactions

	self subclassResponsibility ! !


!Account methodsFor: 'composition' stamp: 'HAW 5/25/2019 12:24:04'!
addedTo: aPortfolio

	self subclassResponsibility ! !


!classDefinition: #Portfolio category: 'Portfolio-Solucion'!
Account subclass: #Portfolio
	instanceVariableNames: 'accounts parents'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 11:49:20'!
accountsIncludes: anAccount

	^accounts includes: anAccount ! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 12:05:04'!
accountsIsEmpty
	
	^accounts isEmpty ! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 11:49:06'!
accountsSize
	
	^accounts size! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 12:19:20'!
add: accountToAdd

	self assertCanAdd: accountToAdd.
		
	accounts add: accountToAdd.
	accountToAdd addedTo: self 
	! !

!Portfolio methodsFor: 'accounts management' stamp: 'HAW 5/25/2019 12:17:31'!
rootParents
	
	| rootParents |
	
	rootParents := Set new.
	self addRootParentsTo: rootParents.
	
	^ rootParents! !


!Portfolio methodsFor: 'initialization' stamp: 'HAW 5/25/2019 12:03:18'!
initialize

	accounts := OrderedCollection new.
	parents := OrderedCollection new.! !


!Portfolio methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:19:36'!
balance
	
	^accounts sum: [ :anAccount | anAccount balance ] ifEmpty: [ 0 ]! !


!Portfolio methodsFor: 'transactions' stamp: 'HAW 5/25/2019 11:42:55'!
addTransactionsTo: aCollectionOfTransactions

	accounts do: [ :anAccount | anAccount addTransactionsTo: aCollectionOfTransactions ]! !

!Portfolio methodsFor: 'transactions' stamp: 'HAW 5/25/2019 11:38:32'!
transactions
	
	| transactions |
	
	transactions := OrderedCollection new.
	accounts do: [ :anAccount | anAccount addTransactionsTo: transactions ].
	
	^transactions ! !


!Portfolio methodsFor: 'composition' stamp: 'HAW 5/25/2019 12:02:59'!
addedTo: aPortfolio 
	
	parents add: aPortfolio ! !


!Portfolio methodsFor: 'testing' stamp: 'HAW 5/25/2019 12:20:56'!
anyRootParentIsComposedBy: accountToAdd

	^self rootParents anySatisfy: [ :aParent | aParent isComposedBy: accountToAdd]! !

!Portfolio methodsFor: 'testing' stamp: 'HAW 5/25/2019 11:28:29'!
hasRegistered: aTransaction

	^accounts anySatisfy: [ :anAccount | anAccount hasRegistered: aTransaction ]! !

!Portfolio methodsFor: 'testing' stamp: 'HAW 5/29/2019 16:24:54'!
isComposedBy: anAccount

	^ self = anAccount or: [ accounts anySatisfy: [ :composedAccount | (composedAccount isComposedBy: anAccount) or: [ anAccount isComposedBy: composedAccount ]]]! !


!Portfolio methodsFor: 'account management - private' stamp: 'HAW 5/25/2019 12:17:31'!
addRootParentsTo: rootParents

	parents 
		ifEmpty: [ rootParents add: self ] 
		ifNotEmpty: [ parents do: [ :aParent | aParent addRootParentsTo: rootParents ]]! !

!Portfolio methodsFor: 'account management - private' stamp: 'HAW 5/25/2019 12:20:36'!
assertCanAdd: accountToAdd

	(self anyRootParentIsComposedBy: accountToAdd) ifTrue: [ self signalCanNotAddAccount ].
! !

!Portfolio methodsFor: 'account management - private' stamp: 'HAW 5/25/2019 11:48:34'!
signalCanNotAddAccount
	
	self error: self class canNotAddAccountErrorMessage! !


!Portfolio methodsFor: 'do' stamp: 'LB 10/24/2021 22:15:46'!
doFor: aReport
	
	^aReport doForPortfolio: self.! !

!Portfolio methodsFor: 'do' stamp: 'LB 10/24/2021 22:18:13'!
doForEachChildAccount: aReport 
	
	accounts do: [:account | aReport doForEachChildAccount: account].! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Portfolio class' category: 'Portfolio-Solucion'!
Portfolio class
	instanceVariableNames: ''!

!Portfolio class methodsFor: 'as yet unclassified' stamp: 'HAW 5/25/2019 11:48:55'!
canNotAddAccountErrorMessage
	
	^'Can not add repeated account to a portfolio'! !

!Portfolio class methodsFor: 'as yet unclassified' stamp: 'HAW 5/25/2019 11:18:21'!
with: anAccount

	^self new 
		add: anAccount;
		yourself! !

!Portfolio class methodsFor: 'as yet unclassified' stamp: 'HAW 5/25/2019 11:23:59'!
with: anAccount with: anotherAccount

	^self new 
		add: anAccount;
		add: anotherAccount;
		yourself! !


!classDefinition: #ReceptiveAccount category: 'Portfolio-Solucion'!
Account subclass: #ReceptiveAccount
	instanceVariableNames: 'transactions'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!ReceptiveAccount methodsFor: 'initialization' stamp: 'HernanWilkinson 7/13/2011 18:35'!
initialize

	super initialize.
	transactions := OrderedCollection new.! !


!ReceptiveAccount methodsFor: 'transactions' stamp: 'HAW 5/25/2019 11:38:52'!
addTransactionsTo: aCollectionOfTransactions

	aCollectionOfTransactions addAll: transactions ! !

!ReceptiveAccount methodsFor: 'transactions' stamp: 'HernanWilkinson 7/13/2011 18:37'!
register: aTransaction

	transactions add: aTransaction 
! !

!ReceptiveAccount methodsFor: 'transactions' stamp: 'HernanWilkinson 7/13/2011 18:37'!
transactions 

	^ transactions copy! !


!ReceptiveAccount methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:24:46'!
balance

	^transactions 
		inject: 0
		into: [ :currentBalance :transaction | transaction affectBalance: currentBalance ]! !


!ReceptiveAccount methodsFor: 'testing' stamp: 'NR 10/21/2019 18:55:56'!
hasRegistered: aTransaction

	^ transactions includes: aTransaction 
! !

!ReceptiveAccount methodsFor: 'testing' stamp: 'HAW 5/25/2019 11:54:51'!
isComposedBy: anAccount

	^self = anAccount ! !


!ReceptiveAccount methodsFor: 'composition' stamp: 'HAW 5/25/2019 12:03:32'!
addedTo: aPortfolio 
	
	! !


!ReceptiveAccount methodsFor: 'do' stamp: 'LB 10/24/2021 21:44:11'!
doFor: aReport
	
	^aReport doForReceptiveAccount: self.! !

!ReceptiveAccount methodsFor: 'do' stamp: 'LB 10/24/2021 21:46:03'!
doForEachTransaction: aReport
	
	transactions do: [:transaction | aReport doForEachTransaction: transaction]! !


!classDefinition: #AccountTransaction category: 'Portfolio-Solucion'!
Object subclass: #AccountTransaction
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!AccountTransaction methodsFor: 'value' stamp: 'HernanWilkinson 9/12/2011 12:25'!
value 

	self subclassResponsibility ! !


!AccountTransaction methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:25:39'!
affectBalance: aBalance

	self subclassResponsibility ! !


!AccountTransaction methodsFor: 'do' stamp: 'LB 10/24/2021 21:47:49'!
doFor: aReport
	self subclassResponsibility.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'AccountTransaction class' category: 'Portfolio-Solucion'!
AccountTransaction class
	instanceVariableNames: ''!

!AccountTransaction class methodsFor: 'instance creation' stamp: 'NR 10/21/2019 18:54:27'!
register: aValue on: account

	| transaction |
	
	transaction := self for: aValue.
	account register: transaction.
		
	^ transaction! !


!classDefinition: #Deposit category: 'Portfolio-Solucion'!
AccountTransaction subclass: #Deposit
	instanceVariableNames: 'value'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Deposit methodsFor: 'initialization' stamp: 'HernanWilkinson 7/13/2011 18:45'!
initializeFor: aValue

	value := aValue ! !


!Deposit methodsFor: 'value' stamp: 'HernanWilkinson 7/13/2011 18:38'!
value

	^ value! !


!Deposit methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:25:02'!
affectBalance: aBalance

	^aBalance + value ! !


!Deposit methodsFor: 'do' stamp: 'LB 10/24/2021 21:53:09'!
doFor: aReport
	aReport doForDeposit: self.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Deposit class' category: 'Portfolio-Solucion'!
Deposit class
	instanceVariableNames: ''!

!Deposit class methodsFor: 'instance creation' stamp: 'HernanWilkinson 7/13/2011 18:38'!
for: aValue

	^ self new initializeFor: aValue ! !


!classDefinition: #TransferLeg category: 'Portfolio-Solucion'!
AccountTransaction subclass: #TransferLeg
	instanceVariableNames: 'transfer'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!TransferLeg methodsFor: 'as yet unclassified' stamp: 'LB 10/21/2021 10:26:39'!
initializeFor: aTransfer
	
	transfer := aTransfer.! !

!TransferLeg methodsFor: 'as yet unclassified' stamp: 'LB 10/21/2021 10:41:59'!
transfer
	
	^transfer! !

!TransferLeg methodsFor: 'as yet unclassified' stamp: 'LB 10/21/2021 21:28:02'!
value
	
	^transfer amount.! !



!TransferLeg methodsFor: 'do' stamp: 'LB 10/24/2021 21:48:20'!
doFor: aReport
	self subclassResponsibility.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'TransferLeg class' category: 'Portfolio-Solucion'!
TransferLeg class
	instanceVariableNames: ''!

!TransferLeg class methodsFor: 'as yet unclassified' stamp: 'LB 10/21/2021 10:25:26'!
for: aTransfer
	
	^ self new initializeFor: aTransfer. ! !

!TransferLeg class methodsFor: 'as yet unclassified' stamp: 'LB 10/21/2021 10:24:44'!
register: aTransfer on: anAccount

	| transaction |
	
	transaction := self for: aTransfer.
	anAccount register: transaction.
		
	^ transaction! !


!classDefinition: #ReceiveLeg category: 'Portfolio-Solucion'!
TransferLeg subclass: #ReceiveLeg
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!ReceiveLeg methodsFor: 'balance' stamp: 'LB 10/21/2021 10:36:38'!
affectBalance: aCurrentBalance
	
	^aCurrentBalance + transfer amount.! !


!ReceiveLeg methodsFor: 'do' stamp: 'LB 10/24/2021 21:52:22'!
doFor: aReport
	aReport doForReceiveLeg: self.! !


!classDefinition: #SendLeg category: 'Portfolio-Solucion'!
TransferLeg subclass: #SendLeg
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!SendLeg methodsFor: 'balance' stamp: 'LB 10/21/2021 10:36:45'!
affectBalance: aCurrentBalance
	
	^aCurrentBalance - transfer amount.! !


!SendLeg methodsFor: 'do' stamp: 'LB 10/24/2021 21:52:56'!
doFor: aReport
	aReport doForSendLeg: self.! !


!classDefinition: #Withdraw category: 'Portfolio-Solucion'!
AccountTransaction subclass: #Withdraw
	instanceVariableNames: 'value'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Withdraw methodsFor: 'initialization' stamp: 'HernanWilkinson 7/13/2011 18:46'!
initializeFor: aValue

	value := aValue ! !


!Withdraw methodsFor: 'value' stamp: 'HernanWilkinson 7/13/2011 18:33'!
value

	^ value! !


!Withdraw methodsFor: 'balance' stamp: 'HAW 5/25/2019 11:25:15'!
affectBalance: aBalance

	^aBalance - value! !


!Withdraw methodsFor: 'do' stamp: 'LB 10/24/2021 21:52:48'!
doFor: aReport
	aReport doForWithdraw: self.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Withdraw class' category: 'Portfolio-Solucion'!
Withdraw class
	instanceVariableNames: ''!

!Withdraw class methodsFor: 'instance creation' stamp: 'HernanWilkinson 7/13/2011 18:33'!
for: aValue

	^ self new initializeFor: aValue ! !


!classDefinition: #Report category: 'Portfolio-Solucion'!
Object subclass: #Report
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Report methodsFor: 'accounts' stamp: 'LB 10/24/2021 22:16:57'!
doForPortfolio: aPortfolio
	
	self subclassResponsibility.
! !

!Report methodsFor: 'accounts' stamp: 'LB 10/24/2021 22:27:35'!
doForReceptiveAccount: aReceptiveAccount
	
	self subclassResponsibility.
! !


!Report methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:26:31'!
doForDeposit: aDeposit
	
	self subclassResponsibility.
! !

!Report methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:27:22'!
doForReceiveLeg: aReceiveLeg
	
	self subclassResponsibility.
! !

!Report methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:27:45'!
doForSendLeg: aSendLeg
	
	self subclassResponsibility.
! !

!Report methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:28:02'!
doForWithdraw: aWithdraw
	
	self subclassResponsibility.
! !


!Report methodsFor: 'iterators' stamp: 'LB 10/24/2021 22:26:56'!
doForEachChildAccount: aChildAccount
	
	self subclassResponsibility.
! !

!Report methodsFor: 'iterators' stamp: 'LB 10/24/2021 22:27:03'!
doForEachTransaction: aTransaction
	
	self subclassResponsibility.
! !



!classDefinition: #AccountSummary category: 'Portfolio-Solucion'!
Report subclass: #AccountSummary
	instanceVariableNames: 'account report'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!AccountSummary methodsFor: 'initialization' stamp: 'LB 10/24/2021 21:58:34'!
initializeFor: anAccount
	
	account:= anAccount.
	report:= OrderedCollection new.
	! !


!AccountSummary methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:01:06'!
doForDeposit: aTransaction
	
	report add: 'Depósito por ', (aTransaction value asString).! !

!AccountSummary methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:01:55'!
doForReceiveLeg: aTransaction

	report add: 'Entrada por transferencia de ', aTransaction value asString.! !

!AccountSummary methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:02:15'!
doForSendLeg: aTransaction
	
	report add: 'Salida por transferencia de ', aTransaction value asString.! !

!AccountSummary methodsFor: 'transactions' stamp: 'LB 10/24/2021 22:00:55'!
doForWithdraw: aTransaction
	
	report add: 'Extracción por ', aTransaction value asString! !


!AccountSummary methodsFor: 'do' stamp: 'LB 10/24/2021 22:03:21'!
do
	account doFor: self.
	report add: 'Balance = '	, (account balance printString).
	^report
	
	! !


!AccountSummary methodsFor: 'iterators' stamp: 'LB 10/24/2021 22:18:42'!
doForEachChildAccount: aChildAccount
	
	^aChildAccount doFor: self.! !

!AccountSummary methodsFor: 'iterators' stamp: 'LB 10/24/2021 21:57:06'!
doForEachTransaction: aTransaction
	
	^aTransaction doFor: self.! !


!AccountSummary methodsFor: 'accounts' stamp: 'LB 10/24/2021 22:17:27'!
doForPortfolio: aPortfolio
	
	aPortfolio doForEachChildAccount: self.
	! !

!AccountSummary methodsFor: 'accounts' stamp: 'LB 10/24/2021 22:04:32'!
doForReceptiveAccount: anReceptiveAccount
	^anReceptiveAccount doForEachTransaction: self.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'AccountSummary class' category: 'Portfolio-Solucion'!
AccountSummary class
	instanceVariableNames: ''!

!AccountSummary class methodsFor: 'as yet unclassified' stamp: 'LB 10/24/2021 21:57:06'!
for: anAccount
	
	^self new initializeFor: anAccount.! !


!classDefinition: #TransferNet category: 'Portfolio-Solucion'!
Report subclass: #TransferNet
	instanceVariableNames: 'account report'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!TransferNet methodsFor: 'initialization' stamp: 'sm 10/24/2021 23:26:59'!
initializeFor: anAccount
	
	account:=anAccount.
	report:=0.
	! !


!TransferNet methodsFor: 'transactions' stamp: 'LB 10/24/2021 21:52:38'!
doForDeposit: aTransaction
	! !

!TransferNet methodsFor: 'transactions' stamp: 'LB 10/24/2021 21:52:22'!
doForReceiveLeg: aTransaction
	
	report := aTransaction affectBalance: report. ! !

!TransferNet methodsFor: 'transactions' stamp: 'LB 10/24/2021 21:52:30'!
doForSendLeg: aTransaction
	
	report := aTransaction affectBalance: report. ! !

!TransferNet methodsFor: 'transactions' stamp: 'LB 10/24/2021 21:52:42'!
doForWithdraw: aTransaction
	! !


!TransferNet methodsFor: 'do' stamp: 'LB 10/24/2021 21:41:19'!
do
	^account doFor: self.
	
	! !


!TransferNet methodsFor: 'iterators' stamp: 'LB 10/24/2021 22:20:32'!
doForEachChildAccount: aChildAccount
	
	^aChildAccount doFor: self! !

!TransferNet methodsFor: 'iterators' stamp: 'LB 10/24/2021 21:47:17'!
doForEachTransaction: aTransaction
	
	^aTransaction doFor: self.! !


!TransferNet methodsFor: 'accounts' stamp: 'LB 10/24/2021 22:20:08'!
doForPortfolio: aPortfolio
	
	aPortfolio doForEachChildAccount: self.
	^report.! !

!TransferNet methodsFor: 'accounts' stamp: 'LB 10/24/2021 21:45:01'!
doForReceptiveAccount: anReceptiveAccount
	anReceptiveAccount doForEachTransaction: self.
	^report.! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'TransferNet class' category: 'Portfolio-Solucion'!
TransferNet class
	instanceVariableNames: ''!

!TransferNet class methodsFor: 'as yet unclassified' stamp: 'sm 10/24/2021 23:22:32'!
for: anAccount
	
	^self new initializeFor: anAccount.! !


!classDefinition: #Transfer category: 'Portfolio-Solucion'!
Object subclass: #Transfer
	instanceVariableNames: 'sendLeg receiveLeg amount'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'Portfolio-Solucion'!

!Transfer methodsFor: 'initialization' stamp: 'LB 10/21/2021 10:36:25'!
amount
	^amount! !

!Transfer methodsFor: 'initialization' stamp: 'LB 10/21/2021 10:18:42'!
initializeFrom: aSender to: aReceiver for: anAmount 
	
	amount := anAmount.
	sendLeg := SendLeg register: self on: aSender.
	receiveLeg := ReceiveLeg register: self on: aReceiver.
! !

!Transfer methodsFor: 'initialization' stamp: 'LB 10/21/2021 10:37:37'!
receiveLeg
	^receiveLeg ! !

!Transfer methodsFor: 'initialization' stamp: 'LB 10/21/2021 10:37:28'!
sendLeg
	^sendLeg! !

"-- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- -- "!

!classDefinition: 'Transfer class' category: 'Portfolio-Solucion'!
Transfer class
	instanceVariableNames: ''!

!Transfer class methodsFor: 'instance creation' stamp: 'LB 10/21/2021 10:54:02'!
from: aSender to: aReceiver for: anAmount 
	
	(anAmount = 0) ifTrue: [self error: 'No se puede hacer una transferencia de valor 0'].
	(aSender = aReceiver) ifTrue: [self error: 'No se puede hacer una transferencia a si mismo'].
	(anAmount < 0) ifTrue: [self error: 'No se puede hacer una transferencia negativa'].
	
	^self new initializeFrom: aSender to: aReceiver for: anAmount.! !
